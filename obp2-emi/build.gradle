plugins {
    id "edu.sc.seis.macAppBundle" version "2.2.2"
}

apply plugin: 'application'
apply plugin: "edu.sc.seis.macAppBundle"

def mainClass = "obp2.ui2.PlugUI"
def properties = new HashMap()
properties.put("apple.laf.useScreenMenuBar", "true")

mainClassName = mainClass


applicationDefaultJvmArgs = ["-XX:+ExitOnOutOfMemoryError"]
properties.each { key, value ->
    applicationDefaultJvmArgs.add("-D${key}=${value}")
}

distributions {
    main {
        baseName = project.name
        version = project.computedVersion
        contents {
            from(project.file("dist")) {
                excludes ["*.pyc"]
                into "."
            }
        }
    }
}

macAppBundle {
    mainClassName = mainClass
    icon = "icon/OBP2.icns"
    properties.each { key, value ->
        javaProperties.put(key, value)
    }
    dmgName = project.name + "-osx-" + project.computedVersion
    bundleJRE = false
    jreHome = ""
}

repositories {
    mavenCentral()
    maven { url "https://repo.eclipse.org/content/groups/releases/" }
    maven { url "https://dl.bintray.com/plug-obp/maven" }
}

dependencies {
    //UI support
    runtime group: 'fr.ensta-bretagne', name: 'obp2-GUI', version: '1.0.0'
    //LTL support
    runtime group: 'fr.ensta-bretagne', name: 'LTL', version: '1.0.0'
    runtime group: 'fr.ensta-bretagne', name: 'obp2-runtime-buchi', version: '1.0.0'
    runtime group: 'fr.ensta-bretagne', name: 'obp2-runtime-buchikripke', version: '1.0.0'
    //runtimes
    runtime group: 'fr.ensta-bretagne', name: 'plug-runtime-via-tcp', version: '1.0.0'
    runtime group: 'fr.ensta-bretagne', name: 'emi-serializer-c', version: '0.9.7'
    runtime group: 'fr.ensta-bretagne', name: 'emi-analysis', version: '0.9.7'
}

build.dependsOn{[createAppZip]}
