plugins {
    id 'java-library'
    id 'application'
    id 'maven-publish'
}

group = "fr.ensta-bretagne"
version='1.1.0_' + new Date().format("yyMMdd")

def properties = new HashMap()

application {
    mainClass = "obp2.cli.PlugRemoteCLI"
    applicationDefaultJvmArgs += ["-XX:+ExitOnOutOfMemoryError"]

    properties.each { key, value ->
        applicationDefaultJvmArgs += "-D${key}=${value}"
    }
}

repositories {
    mavenCentral()
    maven { 
        url = "${OBP2ReadOnlyMavenRepository}"
        allowInsecureProtocol = true 
    }
}

distributions {
    main {
        distributionBaseName = project.name
        version = project.version
        contents {
            duplicatesStrategy = DuplicatesStrategy.EXCLUDE
            into('lib') {
                from configurations.runtimeClasspath
                from jar
            }
        }
    }
}

publishing {
    repositories {
        maven { url = "${OBP2LocalMavenRepository}" }
    }
    publications {
        myDistribution(MavenPublication) {
            artifact distZip
        }
    }
}

dependencies {
    //UI support
    runtimeOnly group: 'fr.ensta-bretagne', name: 'obp2-remote-CLI', version: '1.1.0'
    //LTL support
    runtimeOnly group: 'fr.ensta-bretagne', name: 'LTL', version: '1.0.0'
    runtimeOnly group: 'fr.ensta-bretagne', name: 'obp2-runtime-buchi', version: '1.0.0'
    runtimeOnly group: 'fr.ensta-bretagne', name: 'obp2-runtime-buchikripke', version: '1.0.0'
    //runtimes
    runtimeOnly group: 'fr.ensta-bretagne', name: 'obp2-runtime-remote', version: '1.0.0'
}

