tasks.register('assemble') {
    subprojects.each { dependsOn "${it.name}:assemble" }
}
tasks.register('build') {
    subprojects.each { dependsOn "${it.name}:build" }
}
tasks.register('clean') {
    subprojects.each { dependsOn "${it.name}:clean" }
}
tasks.register('publish') {
    subprojects.each { dependsOn "${it.name}:publish" }
}

version = '1.1.0_' + new Date().format("yyMMdd")
def daily = System.getenv("DAILY")
def computedVersion = daily != null ? "daily-" + new Date().format("yy") : project.version
project.ext.computedVersion =  computedVersion

println "--- Building version:${computedVersion} ---"

subprojects {
    version=computedVersion
    apply plugin: 'distribution'

    repositories {
        mavenCentral()
        maven { 
            url = "${OBP2ReadOnlyMavenRepository}"
            allowInsecureProtocol = true 
            metadataSources {
                artifact()
            }
        }
    }

    apply plugin: 'maven-publish'
    publishing {
        repositories {
            maven { url = "${OBP2LocalMavenRepository}" }
        }
        publications {
            myDistribution(MavenPublication) {
                artifact distZip
            }
        }
    }
}
